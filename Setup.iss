; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "All New Comic Reader"
#define MyAppSafeName "AllNewComicReader"
#define MyAppVersion "1.0"
#define MyAppPublisher "All New Comic Reader"
#define MyAppExeName "AllNewComicReader.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{20AB8AC3-5EE0-4FCD-AD00-66FD635CE8C3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\AllNewComicReader
DefaultGroupName=AllNewComicReader
DisableDirPage=no
DisableWelcomePage=no
DisableProgramGroupPage=yes
OutputDir=bin
OutputBaseFilename={#MyAppSafeName}Setup
Compression=lzma
SolidCompression=yes
ChangesAssociations = yes
WizardSmallImageFile=InnoSetup\WizardTop.bmp
WizardImageFile=InnoSetup\WizSide.bmp
SetupIconFile=InnoSetup\install.ico
UninstallDisplayIcon={app}\{#MyAppExeName}
UninstallIconFile=InnoSetup\uninstall.ico

#include "InnoSetup\redist.iss"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: cbzAssociation; Description: "Associate "".cbz"" files with {#MyAppName}"; GroupDescription: File extensions:
Name: cbrAssociation; Description: "Associate "".cbr"" files with {#MyAppName}"; GroupDescription: File extensions:
Name: cbtAssociation; Description: "Associate "".cbt"" files with {#MyAppName}"; GroupDescription: File extensions:
Name: czAssociation; Description: "Associate "".cz"" files with {#MyAppName}"; GroupDescription: File extensions:

[Registry]
Root: HKCR; Subkey: ".cbz"; ValueType: string; ValueName: ""; ValueData: "ComicFile"; Flags: uninsdeletevalue; Tasks: cbzAssociation 
Root: HKCR; Subkey: ".cbr"; ValueType: string; ValueName: ""; ValueData: "ComicFile"; Flags: uninsdeletevalue; Tasks: cbrAssociation 
Root: HKCR; Subkey: ".cbt"; ValueType: string; ValueName: ""; ValueData: "ComicFile"; Flags: uninsdeletevalue; Tasks: cbtAssociation 
Root: HKCR; Subkey: ".cz"; ValueType: string; ValueName: ""; ValueData: "ComicFile"; Flags: uninsdeletevalue; Tasks: czAssociation 
Root: HKCR; Subkey: "ComicFile"; ValueType: string; ValueName: ""; ValueData: "Comic Archive"; Flags: uninsdeletekey;
Root: HKCR; Subkey: "ComicFile\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\AllNewComicReader.exe,1"
Root: HKCR; Subkey: "ComicFile\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\AllNewComicReader.exe"" ""%1"""

[Files]  
Source: "AllNewComicReader\bin\Deploy\AllNewComicReader.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "AllNewComicReader\bin\Deploy\MyImageMagickXmlFiles\*"; DestDir: "{app}\MyImageMagickXmlFiles\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "AllNewComicReader\bin\Deploy\7z.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "AllNewComicReader\bin\Deploy\7z64.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "AllNewComicReader\bin\Deploy\Magick.NET-Q8-AnyCPU.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "AllNewComicReader\bin\Deploy\SevenZipSharp.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "AllNewComicReader\bin\Deploy\TextDesignerCSLibrary.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ComicThumbnailHandler\Win32\Release\ComicThumbnailHandler32.dll"; DestDir: "{app}"; Flags: regserver 32bit; Check: "not IsWin64"
Source: "ComicThumbnailHandler\x64\Release\ComicThumbnailHandler64.dll"; DestDir: "{app}"; Flags: regserver 64bit; Check: IsWin64
Source: "InnoSetup\Dependecies\vcredist_x86.exe"; Flags: dontcopy;
Source: "InnoSetup\Dependecies\vcredist_x64.exe"; Flags: dontcopy;
Source: "InnoSetup\Dependecies\dotNetFx40_Full_setup.exe"; Flags: dontcopy;   
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
procedure  CurStepChanged(CurStep: TSetupStep);
begin
  if(CurStep=ssInstall) then
  begin
    InstallRedist32();
    InstallRedist64();
  end
 
  if(CurStep=ssPostInstall) then
  begin

    InstallFramework();
  end;
end;